<html>
<head><meta http-equiv=Content-Type content='text/html; charset=UTF-8'>

<title>Virus Source Code Database :: gobble_asm</title>
<meta name="KEYWORDS" content="gobble.asm, virus source, source code, assembly language, assembly programming, hacking, cracking, michaelangelo, stoned, pong, cascade, ambulance, f-prot, mcafee, panda, solomon, anti-virus, anti virus, computer virus">
<meta name="DESCRIPTION" content="Virus Source Code Database - source for gobble.asm">
</head><body><table border=0 cellpadding=0 cellspacing=0 width="100%">
<tr><td valign=top width="25%">
<a href="g2punish_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/g2punish_asm.php">g2punish_asm</a><br>
<a href="g2rainmn_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/g2rainmn_asm.php">g2rainmn_asm</a><br>
<a href="gay-rich_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gay-rich_asm.php">gay-rich_asm</a><br>
<a href="gcae-gen_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gcae-gen_asm.php">gcae-gen_asm</a><br>
<a href="gde_pas.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gde_pas.php">gde_pas</a><br>
<a href="geiger_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/geiger_asm.php">geiger_asm</a><br>
<a href="gen12_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gen12_asm.php">gen12_asm</a><br>
<a href="generic__asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/generic__asm.php">generic__asm</a><br>
<a href="generic_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/generic_asm.php">generic_asm</a><br>
<a href="genius59_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/genius59_asm.php">genius59_asm</a><br>
<a href="getscr_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/getscr_asm.php">getscr_asm</a><br>
<a href="gh_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gh_asm.php">gh_asm</a><br>
<a href="gidra_16_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gidra_16_asm.php">gidra_16_asm</a><br>
<a href="gidra469_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gidra469_asm.php">gidra469_asm</a><br>
<a href="gldbug_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gldbug_asm.php">gldbug_asm</a><br>
<a href="globe_c.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/globe_c.php">globe_c</a><br>
<a href="gloom_c.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gloom_c.php">gloom_c</a><br>
<a href="gobble_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gobble_asm.php">gobble_asm</a><br>
<a href="gold-bu__asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gold-bu__asm.php">gold-bu__asm</a><br>
<a href="gold_bug_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gold_bug_asm.php">gold_bug_asm</a><br>
<a href="gold-bug_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gold-bug_asm.php">gold-bug_asm</a><br>
<a href="goldbug_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/goldbug_asm.php">goldbug_asm</a><br>
<a href="goldgeld_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/goldgeld_asm.php">goldgeld_asm</a><br>
<a href="gomb_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gomb_asm.php">gomb_asm</a><br>
<a href="gotcha17_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gotcha17_asm.php">gotcha17_asm</a><br>
<a href="gotcha1__asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gotcha1__asm.php">gotcha1__asm</a><br>
<a href="gotcha9e_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gotcha9e_asm.php">gotcha9e_asm</a><br>
<a href="gotcha_e_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gotcha_e_asm.php">gotcha_e_asm</a><br>
<a href="gotcha-e_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gotcha-e_asm.php">gotcha-e_asm</a><br>
<a href="gothvamp_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gothvamp_asm.php">gothvamp_asm</a><br>
<a href="gpe_gen_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gpe_gen_asm.php">gpe_gen_asm</a><br>
<a href="gpe-gen_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gpe-gen_asm.php">gpe-gen_asm</a><br>
<a href="greenjok_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/greenjok_asm.php">greenjok_asm</a><br>
<a href="greeting_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/greeting_asm.php">greeting_asm</a><br>
<a href="grither__asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/grither__asm.php">grither__asm</a><br>
<a href="grither_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/grither_asm.php">grither_asm</a><br>
<a href="gtcha17_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gtcha17_asm.php">gtcha17_asm</a><br>
<a href="gun_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gun_asm.php">gun_asm</a><br>
<a href="gunther_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/gunther_asm.php">gunther_asm</a><br>
<a href="guppy_asm.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/guppy_asm.php">guppy_asm</a><br>
</td>
<td valign=top><center><h2>Virus Source Code Database</h2>
<script type="text/javascript">
<!--
                  google_ad_client = "pub-4280558320877613";
                  google_ad_width = 468;
                  google_ad_height = 60;
                  google_ad_format = "468x60_as";
                  google_ad_channel = "5524853059";
                  google_ad_type = "text";
                  google_color_border = "A2CCEE";
                  google_color_bg = "EFEFEF";
                  google_color_link = "000000";
                  google_color_url = "006600";
                  google_color_text = "000000";
        //--></script>
<script type="text/javascript" src="show_ads.js" tppabs="http://pagead2.googlesyndication.com/pagead/show_ads.js"></script>
<p>
<i>This source code is provided for computer programming history.  This source code can be used for good or evil.  It can 
destroy computer data.  Be aware that I am making no claims to authorship or usability of the information found in the 
Virus Source Code Database.  I accept no responsibility for data corruption due to the use of the following information.  The 
information contained on this website is for <b>Information Purposes Only</b>!!!</i><p>
<p>
[ <a href="index.php.htm" tppabs="http://www.totallygeek.com/vscdb/1/index.php">1</a> ]&nbsp;
[ <a href="index-1.php.htm" tppabs="http://www.totallygeek.com/vscdb/a/index.php">a</a> ]&nbsp;
[ <a href="index-2.php.htm" tppabs="http://www.totallygeek.com/vscdb/b/index.php">b</a> ]&nbsp;
[ <a href="index-3.php.htm" tppabs="http://www.totallygeek.com/vscdb/c/index.php">c</a> ]&nbsp;
[ <a href="index-4.php.htm" tppabs="http://www.totallygeek.com/vscdb/d/index.php">d</a> ]&nbsp;
[ <a href="index-5.php.htm" tppabs="http://www.totallygeek.com/vscdb/e/index.php">e</a> ]&nbsp;
[ <a href="index-6.php.htm" tppabs="http://www.totallygeek.com/vscdb/f/index.php">f</a> ]&nbsp;
[ <a href="index-7.php.htm" tppabs="http://www.totallygeek.com/vscdb/g/index.php">g</a> ]&nbsp;
[ <a href="index-8.php.htm" tppabs="http://www.totallygeek.com/vscdb/h/index.php">h</a> ]&nbsp;
[ <a href="index-9.php.htm" tppabs="http://www.totallygeek.com/vscdb/i/index.php">i</a> ]&nbsp;
[ <a href="index-10.php.htm" tppabs="http://www.totallygeek.com/vscdb/j/index.php">j</a> ]&nbsp;
[ <a href="index-11.php.htm" tppabs="http://www.totallygeek.com/vscdb/k/index.php">k</a> ]&nbsp;
[ <a href="index-12.php.htm" tppabs="http://www.totallygeek.com/vscdb/l/index.php">l</a> ]&nbsp;
[ <a href="index-13.php.htm" tppabs="http://www.totallygeek.com/vscdb/m/index.php">m</a> ]&nbsp;
[ <a href="index-14.php.htm" tppabs="http://www.totallygeek.com/vscdb/n/index.php">n</a> ]&nbsp;
[ <a href="index-15.php.htm" tppabs="http://www.totallygeek.com/vscdb/o/index.php">o</a> ]&nbsp;
[ <a href="index-16.php.htm" tppabs="http://www.totallygeek.com/vscdb/p/index.php">p</a> ]&nbsp;
[ <a href="index-17.php.htm" tppabs="http://www.totallygeek.com/vscdb/q/index.php">q</a> ]&nbsp;
[ <a href="index-18.php.htm" tppabs="http://www.totallygeek.com/vscdb/r/index.php">r</a> ]&nbsp;
[ <a href="index-19.php.htm" tppabs="http://www.totallygeek.com/vscdb/s/index.php">s</a> ]&nbsp;
[ <a href="index-20.php.htm" tppabs="http://www.totallygeek.com/vscdb/t/index.php">t</a> ]&nbsp;
[ <a href="index-21.php.htm" tppabs="http://www.totallygeek.com/vscdb/u/index.php">u</a> ]&nbsp;
[ <a href="index-22.php.htm" tppabs="http://www.totallygeek.com/vscdb/v/index.php">v</a> ]&nbsp;
[ <a href="index-23.php.htm" tppabs="http://www.totallygeek.com/vscdb/w/index.php">w</a> ]&nbsp;
[ <a href="index-24.php.htm" tppabs="http://www.totallygeek.com/vscdb/x/index.php">x</a> ]&nbsp;
[ <a href="index-25.php.htm" tppabs="http://www.totallygeek.com/vscdb/y/index.php">y</a> ]&nbsp;
[ <a href="index-26.php.htm" tppabs="http://www.totallygeek.com/vscdb/z/index.php">z</a> ]&nbsp;
</center><p><textarea cols=90 rows=30>

COMMENT @

          This is the assembly language listing for GOBBLE.COM, another
          resident nuisance. GOBBLE produces a simple PacMan (*) image
          on the screen at regular intervals. The PacMan displayed
          then gobbles up four lines of the current screen image, moving
          from the right to the left edge of the screen and buzzing
          merrily. The interval between two PacMan appearances is preset
          to 5 minutes and can be adjusted within a range of 1 to 60 minutes.
          Just enter GOBBLE /?? from the command line, where '??' means
          a number between 1 and 60.

          GOBBLE (C)Copyright 1988 by Joachim Schneider, Zell am See.
          All rights reserved.

          (*) PacMan is a Registered Trademark of Atari Corp.
@
          
_TEXT          SEGMENT BYTE PUBLIC 'CODE'
               ASSUME CS:_TEXT, DS:_TEXT

MAIN           PROC  FAR
               JMP   INSTALL           ; jump to install procedure
               MAIN  ENDP

P_BLOCK        DB 219,248,223,248,219,  7,219,  7,219,7,220,7, 32,7,32,7,32,7
               DB 219,135,219,135,219,135,219,135,219,7,219,7,219,7,32,7,32,7
               DB 219,248,220,248,219,  7,219,  7,219,7,223,7, 32,7,32,7,32,7

LPCT           DB    0,0               ; auxiliary counters for sound timing
COUNT          DW    0                 ; clock tick count
D_COUNT        DW    5460              ; alarm tick count
VSAVE          DD    0                 ; save area for old interrupt vector
SCNT           DW    0                 ; holds current frequency
SW_1           DB    0                 ; sound synchronization aux counter
SCRSEG         DW    0B000H            ; regen buffer base address

GSXX:          PUSHF
               CALL  CS:[VSAVE]        ; call old interrupt handler
               STI
               PUSH  DS
               PUSH  CS
               POP   DS                ; set DS equal to CS
               INC   COUNT             ; bump clock tick count
               PUSH  AX
               MOV   AX,COUNT          ; load current count value
               CMP   AX,D_COUNT        ; reached destination value ?
               POP   AX
               JA    P10               ; yes, set PacMan in motion
               JMP   END_IT            ; else do nothing further
P10:           MOV   COUNT,0           ; re-initialize tick count
               PUSH  DI
               PUSH  SI
               PUSH  DX
               PUSH  CX                ; save caller's registers
               PUSH  BX
               PUSH  AX
               PUSH  ES
               INT   11H               ; get BIOS equipment list
               AND   AL,30H            ; isolate initial video mode bits
               CMP   AL,30H            ; 80-column mono ?
               JNE   S01               ; no, for PC/XT/AT suppose 80-col color
               MOV   AX,0B000H         ; load mono regen buffer address
               JMP   S02
S01:           MOV   AX,0B800H         ; load color regen buffer address
S02:           MOV   ES,AX             ; point ES to regen buffer
               MOV   CX,71             ; # of columns PacMan will move
               MOV   SW_1,0            ; initialize sound timing control
G01:           PUSH  CX
               MOV   BH,8              ; upper image row in BH
               MOV   AL,0A0H           ; load # of bytes for 1 row
               MUL   BH                ; times the line number
               MOV   DI,AX
               SHL   CL,1
               ADD   DI,CX             ; upper left edge address in DI
               MOV   SI,OFFSET P_BLOCK ; set index to start of image
               INC   SW_1              ; bump sound control counter
               CMP   SW_1,2            ; sound effect required ?
               JNZ   G12               ; no, skip sound generation
               MOV   SW_1,0            ; re-initialize sound control
               MOV   CX,20             ; load loop counter
               MOV   SCNT,2000         ; load initial frequency count
G02:           PUSH  CX
               MOV   CX,3
               MOV   BX,SCNT
               CALL  GENSOUND          ; activate speaker
               SUB   SCNT,60           ; increase frequency
               POP   CX
               LOOP  G02               ; play next tone
               JMP   SHORT G11         ; continue animation
G12:           MOV   CX,10000
P03:           LOOP  P03               ; timing loop
G11:           XOR   CX,CX             ; initialize character counters
               MOV   DX,309H           ; load image size; DH=rows, DL=columns
P01:           PUSH  DI                ; saved regen buffer index
P02:           MOVSW                   ; move one character/attribute
               INC   CL                ; bump column count
               CMP   CL,DL             ; one line finished ?
               JNE   P02               ; no, do next column
               INC   CH                ; yes, bump row count
               XOR   CL,CL             ; reset column count
               POP   DI                ; restore regen buffer index
               ADD   DI,0A0H           ; point DI to beginning of next line
               CMP   CH,DH             ; all lines printed ?
               JNE   P01               ; no, continue w/ next line
               POP   CX                ; reload PacMan motion counter
               SUB   CX,2
               JNS   G01               ; repeat until column <= 0
               MOV   BX,0800H          ; line/column position
               MOV   AX,0A0H
               MUL   BH
               MOV   DI,AX
               SHL   BL,1
               XOR   BH,BH
               ADD   DI,BX             ; index into regen buffer
               MOV   CX,240            ; load counter
               MOV   AX,720H           ; load character/attribute
               REP STOSW               ; erase 4 lines where PacMan lives
               POP   ES                ; restore saved registers
               POP   AX
               POP   BX
               POP   CX
               POP   DX
               POP   SI
               POP   DI
END_IT:        POP   DS
               IRET

COMMENT * Put (1193182/freq) in BX
          put time units in CX, 1 unit is approx. 2.6 millisecs *

GENSOUND       PROC  NEAR
               MOV   AL,182
               OUT   67,AL             ; prepare timer for new count
               MOV   AX,BX
               OUT   66,AL             ; send count lsb
               MOV   AL,AH
               OUT   66,AL             ; send count msb
               IN    AL,97      
               OR    AL,3
               OUT   97,AL             ; turn on the speaker
S03:           INC   LPCT              ; wait time unit
               JNZ   S03
               INC   LPCT+1
               LOOP  S03               ; ... wait a little longer ...
               IN    AL,97 
               AND   AL,252
               OUT   97,AL             ; turn speaker off
               RET
               GENSOUND ENDP

         DB    'GOBBLE (C)Copyright 1988 by Joachim Schneider, Zell am See.'
         DB    'All rights reserved.'

INSTALL        PROC  NEAR
               CALL  EVALSW            ; evaluate runtime switch if present
               JC    INST01            ; invalid switch - quit
               MOV   AX,DS
               ADD   AX,10H
               MOV   DS,AX             ; adjust DS to exclude PSP size
               MOV   AX,351CH
               INT   21H                    ; get current INT 1Ch vector
               MOV   WORD PTR VSAVE,BX      ; save offset
               MOV   WORD PTR VSAVE+2,ES    ; and segment
               MOV   DX,OFFSET GSXX         ; load interrupt routine offset
               MOV   AX,251CH
               INT   21H                    ; set new interrupt vector
               MOV   AX,3100H
               MOV   DX,OFFSET INSTALL+100H ; load # of bytes occupied
               MOV   CL,4
               SHR   DX,CL             ; convert from bytes to paragraphs
               INC   DX                ; round value
               INT   21H               ; terminate-stay-resident
INST01:        MOV   AX,4C00H
               INT   21H               ; normal terminate in case of error
               INSTALL ENDP

COMMENT * STR_TO_INT converts the ASCIIZ string in decimal notation
          pointed to by DS:SI to an unsigned binary 16-bit-integer.
          If an invalid digit is encountered or the value is larger
          than 0FFFFH the CY flag will be set. On return, AX contains
          the integer value. *

STR_TO_INT     PROC  NEAR
               PUSH  BX
               PUSH  CX
               PUSH  DX
               XOR   AX,AX             ; clear AX
               MOV   BH,AH
               MOV   CX,10             ; factor for multiplication
STRTI05:       LODSB                   ; load character
               OR    AL,AL             ; end-of-string ?
               JZ    STRTI01           ; yes, nothing to convert
               CMP   AL,20H            ; blank ?
               JZ    STRTI05           ; yes, skip
               CMP   AL,'0'            ; check for digit
               JB    STRTI04           ; error: can't be digit
               CMP   AL,'9'
               JA    STRTI04           ; error: can't be digit
               SUB   AL,'0'            ; convert digit to binary value
STRTI03:       MOV   BL,[SI]           ; load next character
               INC   SI                ; bump index
               OR    BL,BL             ; end-of-string ?
               JZ    STRTI01           ; yes, end conversion
               CMP   BL,'0'            ; does BL contain a digit ?
               JB    STRTI04           ; no, flag error
               CMP   BL,'9'
               JA    STRTI04           ; error - no digit
               SUB   BL,'0'            ; convert BL to binary value
               MUL   CX                ; shift current value by 10
               JC    STRTI02           ; exit if overflow
               ADD   AX,BX             ; add current digit
               JC    STRTI02           ; exit if overflow
               JMP   SHORT STRTI03     ; go read next character
STRTI04:       STC                     ; set CF to 1, i.e. flag an error
               JMP   SHORT STRTI02     ; and exit
STRTI01:       CLC                     ; no error - clear CF
STRTI02:       POP   DX
               POP   CX
               POP   BX                ; restore registers
               RET
               STR_TO_INT ENDP

COMMENT * EVALSW will interpret the given runtime switches if available.
          On return, the CY flag will be set if there was an invalid parm,
          otherwise CF is clear. *

EVALSW         PROC NEAR
               MOV   CL,DS:80H         ; get length of command tail
               XOR   CH,CH
               JCXZ  EW01              ; nothing there, exit
               MOV   DI,81H            ; point DI to 1st character of tail
               CLD
EW05:          MOV   AL,'/'
               REPNZ SCASB             ; scan command tail for slash
               JCXZ  EW01              ; none found - quit
               MOV   SI,DI             ; copy string pointer
               CALL  STR_TO_INT        ; convert string to binary integer
               OR    AX,AX             ; invalid or 0 ?
               JZ    EW04              ; yes, flag error
               CMP   AX,60             ; larger than 60 ?
               JA    EW04              ; yes, flag error
               MOV   BX,1092           ; convert minute count ...
               MUL   BX                ; ... to clock tick count
               MOV   D_COUNT+100H,AX   ; and save destination count
               JMP   SHORT EW01
EW04:          MOV   DX,OFFSET ILLPARM+100H
               MOV   AH,9
               INT   21H               ; display error message
               STC                     ; and flag error
               JMP   SHORT EW08
EW01:          CLC                     ; no error
EW08:          RET
               EVALSW ENDP

ILLPARM        DB    'Error: Minute count must be in range 1 to 60.',7,'$'

               _TEXT  ENDS
               END

</textarea></td></tr></table></body></html>
